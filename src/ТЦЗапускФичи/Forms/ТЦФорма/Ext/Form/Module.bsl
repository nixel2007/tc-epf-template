///////////////////////////////////////////////////////////////////////////////
// В Н И М А Н И Е!
// ПЕРЕМЕННЫЕ, ПРОЦЕДУРЫ И ФУНКЦИИ, НАЧИНАЮЩИЕСЯ С "ТЦ", НЕЛЬЗЯ УДАЛЯТЬ, Т.К.
// ОНИ НЕОБХОДИМЫ ДЛЯ ПРАВИЛЬНОЙ РАБОТЫ ТЕСТ-ЦЕНТРА
//

&НаКлиенте
Перем ТЦКонтекстВыполнения; // Служебная переменная Тест-центра
&НаКлиенте
Перем ТЦИмяОбработчика;     // Служебная переменная Тест-центра

&НаКлиенте
// Переменная хранит ссылку на открытую форму Vanessa-Automation
Перем ФормаVA;

#Область Интерфейс_разработчика_сценария

&НаКлиенте
// Единовременная подготовка данных перед выполнением действия.
// Эта подготовка выполняется только при необходимости и не является
// обязательной.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТЦРезультатВыполнения.
//   Успешно - если при инициализации ошибок не возникло
//   Предупреждение - если возникшие ошибки позволяют продолжить выполнение
//   Ошибка - если возникли ошибки, которые не позволяют продолжить выполнение
//
Функция ТЦИнициализировать() Экспорт
	
	ОткрытьФормуVA();
	
	Возврат ТЦРезультатВыполненияУспешно();
	
КонецФункции // ТЦИнициализировать()

&НаКлиенте
// Выполнение действия.
// В этой функции содержится основной код действия, необходимый для выполнения
// сценария.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТЦРезультатВыполнения.
//   Успешно - если при инициализации ошибок не возникло
//   Предупреждение - если возникшие ошибки позволяют продолжить выполнение
//   Ошибка - если возникли ошибки, которые не позволяют продолжить выполнение
//
Функция ТЦВыполнить() Экспорт
	
	ЗаполнитьНастройкиVA();
	ПеренестиНастройкиРолиВКонтекстVA();
	ПодготовитьVAКВыполнениюСценариев();
	
	НачатьВыполнениеСценария();
	ПодключитьОбработчикОжидания("ПроверитьПрерываниеСценария", 10, Ложь);
	
	Возврат ТЦРезультатВыполненияПродолжить("ОбработчикОжиданияВыполненияСценариев", 1, Ложь);
		
КонецФункции // ТЦВыполнить()

&НаКлиенте
// Процедура вызывается перед началом прогона сценария Vanessa-Automation.
// Здесь можно установить дополнительные данные КонтекстСохраняемый и самой VA.
//
Процедура ПередНачаломВыполненияСценарияVA()
	// Дополнительная логика
КонецПроцедуры

&НаКлиенте
// Единовременное удаление созданных при инициализации данных, после выполнения
// действия. Это удаление выполняется только при необходимости и не является
// обязательным.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТЦРезультатВыполнения.
//   Успешно - если при инициализации ошибок не возникло
//   Предупреждение - если возникшие ошибки позволяют продолжить выполнение
//   Ошибка - если возникли ошибки, которые не позволяют продолжить выполнение
//
Функция ТЦУдалитьДанные() Экспорт
	
	Возврат ТЦРезультатВыполненияУспешно();
	
КонецФункции // ТЦУдалитьДанные()

#КонецОбласти

#Область Вспомогательные_процедуры_и_функции_для_разработчика_сценария

&НаКлиенте
// Записать значение показателя производительности
//
// Параметры:
//  ИмяПоказателя - Строка, произвольное имя показателя производительности
//  ЗначениеПоказателя - Число, значение показателя
//
Процедура ТЦЗаписатьПоказатель(ИмяПоказателя, ЗначениеПоказателя)
	
	МодульТЦКлиент = ТЦПолучитьОбщийМодульПоИмени("ТЦКлиент");
	Если МодульТЦКлиент <> Неопределено Тогда
		МодульТЦКлиент.ДобавитьРезультат(ТЦКонтекст(), ИмяПоказателя, ЗначениеПоказателя);
	КонецЕсли;
	
КонецПроцедуры // ТЦЗаписатьПоказатель()

&НаКлиенте
// Завершить выполнение действия
//
// Параметры:
//  Результат - ПеречислениеСсылка.ТЦРезультатВыполнения
//  ВозниклоИсключение - Булево, при выполнении обработчика ожидания
//                 возникла и обработана исключительная ситуация
//                 указывать этот параметр явно не следует
//
Процедура ТЦЗавершитьВыполнение(Результат, ВозниклоИсключение = Ложь, ТекстОшибкиОбработки = "")
	
	ОтключитьОбработчикОжидания("ТЦОбработчикВыполнения");
	ФормаВРМ = ТЦКонтекст().ФормаВРМ;
	
	Если ФормаВРМ <> Неопределено Тогда
		ФормаВРМ.РезультатВыполнения = Результат;
		ФормаВРМ.ВозниклоИсключение = ВозниклоИсключение;
		ФормаВРМ.ТекстОшибкиОбработки = ТекстОшибкиОбработки;
	КонецЕсли;
	
	ТЦКонтекстВыполнения.ТекущийРезультатВыполнения = Результат;
	ТЦКонтекстВыполнения.ВозниклоИсключение = ВозниклоИсключение;
	ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки = ТекстОшибкиОбработки;
	
КонецПроцедуры // ТЦЗавершитьВыполнение()

&НаКлиенте
// Номер ВРМ, уникальный в рамках сценария
//
// Возвращаемое значение:
//  Число - номер текущего ВРМ
//
Функция ТЦНомерВРМ()
	
	Возврат ТЦКонтекст().ВРМ.Номер;
	
КонецФункции // ТЦНомерВРМ()

&НаСервере
Функция ТЦРеальныйНомерВРМ(ПредварительныйФильтрПоИмениКомпьютера = Ложь)
	
	Роль = ТЦИмяРоли();
	НомерВРМ = ТЦНомерВРМ();
	ИмяКомпьютера = ТЦИмяКомпьютера();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЦСценарийЗапуска.Компьютер,
	|	ТЦСценарийЗапуска.Компьютер.Наименование КАК ИмяКомпьютера,
	|	ТЦСценарийЗапуска.НомерСессии,
	|	ТЦСценарийЗапуска.Клиент,
	|	ТЦСценарийЗапуска.Клон,
	|	ТЦСценарийЗапуска.Ид КАК Ид,
	|	ТЦСценарийЗапуска.Роль,
	|	ТЦСценарийЗапуска.Пользователь,
	|	ТЦСценарийЗапуска.Агент,
	|	ТЦСценарийЗапуска.Номер,
	|	ТЦСценарийЗапуска.Запущен,
	|	ТЦСценарийЗапуска.Состояние,
	|	ТЦСценарийЗапуска.ПродолжатьРаботуМеждуИтерациями
	|ИЗ
	|	РегистрСведений.ТЦСценарийЗапуска КАК ТЦСценарийЗапуска
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ид";
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	Если ПредварительныйФильтрПоИмениКомпьютера Тогда
		ТЗ = ТЗ.Скопировать(Новый Структура("ИмяКомпьютера", ИмяКомпьютера));
	КонецЕсли;
	
	Строка = ТЗ.НайтиСтроки(Новый Структура("Роль, Номер, ИмяКомпьютера", Роль, НомерВРМ, ИмяКомпьютера))[0]; 
	РеальныйНомер = ТЗ.Индекс(Строка);
	
	Возврат РеальныйНомер;
	
КонецФункции

&НаКлиенте
// Ссылка на сценарий
//
// Возвращаемое значение:
//  СправочникСсылка.ТЦСценарии
//
Функция ТЦСценарий()
	
	Возврат ТЦКонтекст().ВРМ.Сценарий;
	
КонецФункции // ТЦСценарий()

&НаКлиенте
// Возвращает коэффициент интенсивности текущего сценария
//
// Возвращаемое значение:
//  Число
//
Функция ТЦКоэффициентИнтенсивности()
	
	Возврат ТЦКонтекст().КоэффициентИнтенсивности;
	
КонецФункции // ТЦСценарий()

&НаКлиенте
// Получить имя текущей роли
//
// Возвращаемое значение:
//  Строка - имя текущей роли
//
Функция ТЦИмяРоли()
	
	Возврат ТЦКонтекст().ВРМ.ИмяРоли;
	
КонецФункции // ТЦИмяРоли()

&НаКлиенте
// Получить имя текущей роли
//
// Возвращаемое значение:
//  Строка - имя текущей роли
//
Функция ТЦРоль()
	
	Возврат ТЦКонтекст().ВРМ.Роль;
	
КонецФункции // ТЦРоль()

&НаКлиенте
// Получить имя текущего пользователя
//
// Возвращаемое значение:
//  Строка - имя текущего пользователя
//
Функция ТЦИмяПользователя()
	
	Возврат ТЦКонтекст().ВРМ.Пользователь;
	
КонецФункции // ТЦИмяПользователя()

&НаСервереБезКонтекста
// Возвращает пароль указанного пользователя из справочника "ТЦПользователи".
// В случае отсутствия пользователя в справочнике ТЦПользователи бросает исключение.
//
// Параметры:
//   ИмяПользователя - Строка - ИмяПользователя
//
//  Возвращаемое значение:
//   Строка - Пароль пользователя
//
Функция ТЦПарольПользователя(ИмяПользователя)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЦПользователи.Пароль
	|ИЗ
	|	Справочник.ТЦПользователи КАК ТЦПользователи
	|ГДЕ
	|	ТЦПользователи.Имя = &Имя
	|	И НЕ ТЦПользователи.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Имя", ИмяПользователя);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Пароль;
	Иначе
		ВызватьИсключение "Не обнаружен пароль пользователя " + ИмяПользователя;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
// Получить имя текущего компьютера
//
// Возвращаемое значение:
//  Строка - имя текущего компьютера
//
Функция ТЦИмяКомпьютера()
	
	Возврат ТЦКонтекст().ВРМ.Компьютер;
	
КонецФункции // ТЦИмяКомпьютера()

&НаСервере
// Приостанавливает выполнение кода на указанное количество секунд.
//
// Параметры:
//   РазмерПаузы - Число - Количество секунд для паузы (целое)
//
Процедура ТЦПауза(РазмерПаузы)
	
	Инструменты = КипВнешнийКомпонент.ПолучитьИнструменты();	
	КипВнешнийКомпонент.Пауза(Инструменты, РазмерПаузы * 1000);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
// Перемешивает переданный массив, используя генератор случайных чисел.
//
// Параметры:
//   Массив - Массив - Исходный массив для перемешивания
//
Процедура ТЦПеремешатьМассив(Массив)
	ГСЧ = Новый ГенераторСлучайныхЧисел;
	Для сч = 1 По Массив.ВГраница() Цикл
		НовыйСч = ГСЧ.СлучайноеЧисло(0, Массив.ВГраница());
		
		Врем = Массив[НовыйСч];
		Массив[НовыйСч] = Массив[сч];
		Массив[сч] = Врем;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
// Получить результат выполнения "Успешно"
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТЦРезультатВыполнения.Успешно
//
Функция ТЦРезультатВыполненияУспешно()
	
	Возврат ТЦКонтекст().РезультатВыполнения.Успешно;
	
КонецФункции // ТЦРезультатВыполненияУспешно()

&НаКлиенте
// Получить результат выполнения "Предупреждение"
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТЦРезультатВыполнения.Предупреждение
//
Функция ТЦРезультатВыполненияПредупреждение()
	
	Возврат ТЦКонтекст().РезультатВыполнения.Предупреждение;
	
КонецФункции // ТЦРезультатВыполненияПредупреждение()

&НаКлиенте
// Получить результат выполнения "Ошибка"
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТЦРезультатВыполнения.Ошибка
//
Функция ТЦРезультатВыполненияОшибка()
	
	Возврат ТЦКонтекст().РезультатВыполнения.Ошибка;
	
КонецФункции // ТЦРезультатВыполненияОшибка()

&НаКлиенте
// Получить результат выполнения "Продолжить" и подключить обработчик
// ТЦОбработчикВыполнения с указанным интервалом, который в свою очередь,
// Периодически выполняет функцию указанную в параметре Обработчик.
//
// Параметры:
//  Интервал - Число, через которое будет вызываться обработчик
//  Однократно - Булево, Признак однократного выполнения обработчика ожидания
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТЦРезультатВыполнения.Продолжить
//
Функция ТЦРезультатВыполненияПродолжить(Обработчик, Интервал, Однократно = Ложь)
	
	ТЦИмяОбработчика = Обработчик;
	ПодключитьОбработчикОжидания("ТЦОбработчикВыполнения", Интервал, Однократно);
	Возврат ТЦКонтекст().РезультатВыполнения.Продолжить;
	
КонецФункции // ТЦРезультатВыполненияОшибка()

&НаКлиенте
// Получить статус сообщения "Информация"
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТЦСтатусСообщения.Информация
//
Функция ТЦСтатусСообщенияИнформация()
	
	Возврат ТЦКонтекст().СтатусСообщения.Информация;
	
КонецФункции // ТЦСтатусСообщенияИнформация()

&НаКлиенте
// Получить статус сообщения "Предупреждение"
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТЦСтатусСообщения.Предупреждение
//
Функция ТЦСтатусСообщенияПредупреждение()
	
	Возврат ТЦКонтекст().СтатусСообщения.Предупреждение;
	
КонецФункции // ТЦСтатусСообщенияПредупреждение()

&НаКлиенте
// Получить статус сообщения "Ошибка"
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТЦСтатусСообщения.Ошибка
//
Функция ТЦСтатусСообщенияОшибка()
	
	Возврат ТЦКонтекст().СтатусСообщения.Ошибка;
	
КонецФункции // ТЦСтатусСообщенияОшибка()

#КонецОбласти

#Область Служебные_процедуры_и_функции_Тест_Центра

&НаКлиенте
// Обработчик команды "Выполнить".
// Выполняет инициализацию, действие и удаление созданных данных.
//
Процедура ВыполнитьДействие(Команда)
	
	ТЦВыполнитьТестирование(Ложь, Истина);
	
КонецПроцедуры // ВыполнитьДействие()

&НаКлиенте
// Обработчик выполнения действия в случае возврата из ТЦВыполнить результата
// ПеречислениеСсылка.ТЦРезультатВыполнения.ТЦПродолжить
//
Процедура ТЦОбработчикВыполнения()
	
	Попытка
		Результат = Вычислить(ТЦИмяОбработчика + "()");
	Исключение
		
		Если Не ТЦЭтоИзолированныйРежимРаботы Тогда
			
			МодульТЦОбщий = ТЦПолучитьОбщийМодульПоИмени("ТЦОбщий");
			Если МодульТЦОбщий <> Неопределено Тогда
				МодульТЦОбщий.ЗаписатьВЖурнал(ИнформацияОбОшибке(), "ВП");
				ТЦЗавершитьВыполнение(
					ТЦКонтекст().РезультатВыполнения.Неопределено,
					Истина,
					МодульТЦОбщий.ИнформациюОбОшибкеВСтроку(ИнформацияОбОшибке()));
			КонецЕсли;
			
		// дейстия в изолированном режиме работы	
		Иначе
			ТЦЗавершитьВыполнение(
					ТЦКонтекст().РезультатВыполнения.Неопределено,
					Истина,
					ТЦИнформациюОбОшибкеВСтроку(ИнформацияОбОшибке()));			
		КонецЕсли;
				
	КонецПопытки;
	
КонецПроцедуры // ТЦОбработчикВыполнения()

&НаКлиенте
// Получить контекст выполнения обработки
//
// Возвращаемое значение:
//  Структура - см. ТЦСервер.СоздатьКонтекстВыполненияОбработки()
//
Функция ТЦКонтекст() Экспорт
	
	Если ТЦКонтекстВыполнения = Неопределено Тогда
		
		Если НЕ ТЦЭтоИзолированныйРежимРаботы Тогда
			МодульТЦСервер = ТЦПолучитьОбщийМодульПоИмени("ТЦСервер");
			Если МодульТЦСервер <> Неопределено Тогда
				ТЦКонтекстВыполнения = МодульТЦСервер.СоздатьКонтекстВыполненияОбработки();
			КонецЕсли;
		Иначе
			ТЦКонтекстВыполнения = ТЦСоздатьКонтекстДляИзолированногоИсполнения();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТЦКонтекстВыполнения;
	
КонецФункции // ТЦКонтекст()

&НаКлиенте
// Загрузить параметры обработки и формы
//
// Параметры:
//  ПараметрыЗагрузки - ХранилищеЗначения
//
Процедура ТЦЗагрузить(ПараметрыЗагрузки) Экспорт
	
	Если НЕ ТЦЭтоИзолированныйРежимРаботы Тогда
		ТЦЗагрузитьНаСервере(ПараметрыЗагрузки);
	КонецЕсли;
	
	ТЦКонтекст();
	
КонецПроцедуры // ТЦЗагрузить()

&НаСервере
// Загрузить параметры обработки.
// Во время загрузки устанавливаются ранее сохраненные значения реквизитов
//
// Параметры:
//  АрхивЗначений - ХранилищеЗначения, загружаемые данные
//
Процедура ТЦЗагрузитьНаСервере(АрхивЗначений)
	
	МодульТЦСервер = ТЦПолучитьОбщийМодульПоИмени("ТЦСервер");
	Если МодульТЦСервер <> Неопределено Тогда
		
		ТекущийОбъект = РеквизитФормыВЗначение("ТЦОбъект");
	
		МодульТЦСервер.ЗагрузитьРеквизитыОбработки(ТекущийОбъект, АрхивЗначений);
		ЗначениеВРеквизитФормы(ТекущийОбъект, "ТЦОбъект");
		
	КонецЕсли;
	
КонецПроцедуры // ТЦЗагрузитьНаСервере()

&НаКлиенте
// Сохранить значения реквизитов для возможности последующей загрузки
//
// Возвращаемое значение:
//  ХранилищеЗначения - запакованые значения реквизитов
//
Функция ТЦСохранить() Экспорт
	
	Если ТЦЭтоИзолированныйРежимРаботы Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ТЦСохранитьНаСервере();
	
КонецФункции // ТЦСохранить()

&НаСервере
// Сохранить значения реквизитов для возможности последующей загрузки
//
// Возвращаемое значение:
//  ХранилищеЗначения - запакованые значения реквизитов
//
Функция ТЦСохранитьНаСервере()
	
	МодульТЦСервер = ТЦПолучитьОбщийМодульПоИмени("ТЦСервер");
	Если МодульТЦСервер <> Неопределено Тогда
		ТекущийОбъект = РеквизитФормыВЗначение("ТЦОбъект");
		Возврат МодульТЦСервер.СохранитьРеквизитыОбработки(ТекущийОбъект);
	КонецЕсли;
	
КонецФункции // ТЦСохранитьНаСервере()

&НаКлиенте
// Получить результат выполнения тестирования
//
// Возвращаемое значение:
//  Соответствие - показатели и их значения
//
Функция ТЦПолучитьРезультат() Экспорт
	
	Возврат ТЦКонтекст().Результаты;
	
КонецФункции // ТЦПолучитьРезультат()

&НаКлиенте
// Закрыть форму и вернуть значение Истина
//
Процедура ТЦОК(Команда)
	
	Закрыть(Истина);
	
КонецПроцедуры // ТЦОК()

&НаСервере
// Обработчик события формы ПриСозданииНаСервере
Процедура ТЦПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Определим, присутствует ли подсистема ТестЦентр
	ТЦЭтоИзолированныйРежимРаботы = Ложь;
	ПодсистемаТестЦентр = Метаданные.Подсистемы.Найти("ТестЦентр");
	Если ПодсистемаТестЦентр = Неопределено Тогда
		ТЦЭтоИзолированныйРежимРаботы = Истина;
		
		СисИнфо = Новый СистемнаяИнформация;
		Если СисИнфо.ТипПлатформы = ТипПлатформы.Windows_x86 ИЛИ СисИнфо.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
			ТЦОбъект.ТЦИмяФайлаЖурналаТестирования = "C:\Logs1C\TestResults.txt";
		ИначеЕсли СисИнфо.ТипПлатформы = ТипПлатформы.Linux_x86 ИЛИ СисИнфо.ТипПлатформы = ТипПлатформы.Linux_x86_64 Тогда
			ТЦОбъект.ТЦИмяФайлаЖурналаТестирования = "/var/log/1C/TestResults.txt";
		ИначеЕсли СисИнфо.ТипПлатформы = ТипПлатформы.MacOS_x86 ИЛИ СисИнфо.ТипПлатформы = ТипПлатформы.MacOS_x86_64 Тогда
			ТЦОбъект.ТЦИмяФайлаЖурналаТестирования = "/var/log/1C/TestResults.txt";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события формы ПриОткрытии
Процедура ТЦПриОткрытии(Отказ)
	
	Если Найти(Нрег(ПараметрЗапуска), Нрег("TCLaunchTest")) <> 0 И ТЦЭтоИзолированныйРежимРаботы Тогда
		
		Элементы.ТЦОбщийРезультат.Видимость	= Ложь;
		Элементы.ТЦРезультаты.Видимость		= Ложь;
		ТЦВыполнитьТестирование();
		
	ИначеЕсли НЕ ТЦЭтоИзолированныйРежимРаботы Тогда
		
		Элементы.ТЦОбщийРезультат.Видимость	= Ложь;
		Элементы.ТЦРезультаты.Видимость		= Ложь;
		
	Иначе
		
		Элементы.ТЦОбщийРезультат.Видимость	= Истина;
		Элементы.ТЦРезультаты.Видимость		= Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Служебные_процедуры_и_функции_без_Тест_Центра

&НаКлиенте
// Выполняет инициализацию, действие и удаление созданных данных в изолированном режиме работы.
Процедура ТЦВыполнитьТестирование(ЗавершатьРаботу = Истина, ПоказыватьОшибкиИСообщения = Ложь)
	
	ТЦКонтекст().ПоказыватьОшибкиИСообщения = ПоказыватьОшибкиИСообщения;
	ТЦКонтекстВыполнения.ЗавершатьРаботуПослеВыполнения = ЗавершатьРаботу;
	
	Если ТЦЭтоИзолированныйРежимРаботы Тогда
		Элементы.ТЦОбщийРезультат.Видимость	= ПоказыватьОшибкиИСообщения;
		Элементы.ТЦРезультаты.Видимость		= ПоказыватьОшибкиИСообщения;
		ТЦЗаписатьСистемнуюИнформациюВФайлНаСервере();
	КонецЕсли;
	
	ТЦКонтекстВыполнения.МассивДействий = Новый Массив;
	ТЦКонтекстВыполнения.МассивДействий.Добавить("ТЦИнициализировать()");
	ТЦКонтекстВыполнения.МассивДействий.Добавить("ТЦВыполнить()");
	ТЦКонтекстВыполнения.МассивДействий.Добавить("ТЦУдалитьДанные()");
	
	Для Каждого ТекущееДействие Из ТЦКонтекстВыполнения.МассивДействий Цикл
		
		РезультатВыполнения = ТЦВыполнитьДействиеТеста(ТекущееДействие);

		Если РезультатВыполнения = ТЦКонтекстВыполнения.РезультатВыполнения.Продолжить
			ИЛИ РезультатВыполнения = ТЦРезультатВыполненияОшибка()
			ИЛИ ТЦКонтекстВыполнения.ВозниклоИсключение Тогда

			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТЦЭтоИзолированныйРежимРаботы Тогда
		Если ТЦКоличествоОшибок = 0 Тогда
			ТЦОбщийРезультат = "Тесты пройдены без ошибок";
		Иначе 
			ТЦОбщийРезультат = "Количество ошибок: " + ТЦКоличествоОшибок;
		КонецЕсли;			
	КонецЕсли;
	
	Если ТЦКонтекстВыполнения.ЗавершатьРаботуПослеВыполнения Тогда
		ЗавершитьРаботуСистемы(Ложь, Ложь);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Обработчик заверщения выполнения
Процедура ТЦОжиданиеЗавершенияВыполнения()
	
	ЗавершитьВыполнение = Ложь;
	
	Если ТЦЭтоИзолированныйРежимРаботы Тогда
		ТЦКонтекстВыполнения.ОкончаниеТекущегоЗамера = ТЦПолучитьВремяДляЗамера();
		Длительность = ТЦКонтекстВыполнения.ОкончаниеТекущегоЗамера-ТЦКонтекстВыполнения.НачалоТекущегоЗамера;
		Если ТЦЕстьВремяВМиллисекундах() Тогда
			Длительность = Длительность / 1000;
		КонецЕсли;
	КонецЕсли;
	
	ТекущийРезультатВыполнения = ТЦКонтекст().ТекущийРезультатВыполнения;
	ТекущаяОперация = ТЦКонтекстВыполнения.ТекущаяОперация;
	
	// Возникло исключение
	// или выполнение действия вернуло ошибку
	Если ТЦКонтекстВыполнения.ВозниклоИсключение ИЛИ ТекущийРезультатВыполнения = ТЦРезультатВыполненияОшибка() Тогда
		
		ЗавершитьВыполнение = Истина;
		Пояснение = ?(ТекущийРезультатВыполнения = ТЦРезультатВыполненияОшибка(), "Функция вернула ошибку. ", "");
		ТекстОшибкиПользователю = Пояснение;
		
		Если ТЦЭтоИзолированныйРежимРаботы Тогда
			ТекстОшибкиПользователю = Пояснение + ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки + " Длительность="
				+ Формат(Длительность, "ЧДЦ=2; ЧРГ=.; ЧН=0") + " с";

			ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки = "Действие=" + ТекущаяОперация + " Длительность="
				+ Формат(Длительность, "ЧДЦ=2; ЧРГ=.; ЧН=0") + " с"
				+ " ОшибкаПриВыполнении=" + Символы.ПС + Пояснение + ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки;
				
		Иначе
			ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки = "Возникла ошибка при выполнении " + ТекущаяОперация +
														Символы.ПС + Пояснение + ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки;
		КонецЕсли;
			
		Если ТЦКонтекстВыполнения.ПоказыватьОшибкиИСообщения И НЕ ТЦЭтоИзолированныйРежимРаботы Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки;
			Сообщение.Сообщить();
		КонецЕсли;
	
		ТЦЗаписатьВЖурнал(ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки, "Тест-центр", ТЦСтатусСообщенияОшибка());
		Если ТЦЭтоИзолированныйРежимРаботы Тогда
			ТЦЗаписьРезультатовВФайлНаСервере(ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки);
			ТЦКоличествоОшибок = ТЦКоличествоОшибок + 1;
			ТЦПоказазатьОшибку(ТекущаяОперация, ТекстОшибкиПользователю);
		КонецЕсли;
	
	// Завершилась часть теста
	ИначеЕсли ТекущийРезультатВыполнения <> Неопределено
		И ТекущийРезультатВыполнения <> ТЦКонтекстВыполнения.РезультатВыполнения.Неопределено
		И ТекущийРезультатВыполнения <> ТЦКонтекстВыполнения.РезультатВыполнения.Продолжить Тогда

		Если ТЦЭтоИзолированныйРежимРаботы Тогда
			ТЦЗаписьРезультатовВФайлНаСервере("Действие=" + ТекущаяОперация + " Длительность="
				+ Формат(Длительность, "ЧДЦ=2; ЧРГ=.; ЧН=0") + " с");
			ТЦПоказазатьЗамер(ТекущаяОперация, Длительность);
		КонецЕсли;
			
		Индекс = ТЦКонтекстВыполнения.МассивДействий.Найти(ТекущаяОперация);
		// Произошла ошибка
		Если Индекс = Неопределено Тогда
			ЗавершитьВыполнение = Истина;
			
			Если ТЦЭтоИзолированныйРежимРаботы Тогда
				
				ТекстОшибки = "Ошибка продолжения теста. Операция """ + ТЦКонтекстВыполнения.ТекущаяОперация + """ не найдена в ТЦМассивДействий";
				ТЦЗаписьРезультатовВФайлНаСервере(ТекстОшибки);
				ТЦКоличествоОшибок = ТЦКоличествоОшибок + 1;
				ТЦПоказазатьОшибку(ТекущаяОперация, ТекстОшибки);
				
			КонецЕсли;
			
		// Все операции выполнены	
		ИначеЕсли Индекс = ТЦКонтекстВыполнения.МассивДействий.ВГраница() Тогда
			ЗавершитьВыполнение = Истина;
			
		// Продолжаем выполнение операций
		Иначе
			ОтключитьОбработчикОжидания("ТЦОжиданиеЗавершенияВыполнения");
			
			МассивДействий = ТЦКонтекстВыполнения.МассивДействий;
			Для Сч = Индекс + 1 По МассивДействий.ВГраница() Цикл
				
				ТекущаяОперация = МассивДействий[Сч];
				РезультатВыполнения = ТЦВыполнитьДействиеТеста(ТекущаяОперация);
				
				Если РезультатВыполнения = ТЦКонтекстВыполнения.РезультатВыполнения.Продолжить
					ИЛИ РезультатВыполнения = ТЦРезультатВыполненияОшибка()
					ИЛИ ТЦКонтекстВыполнения.ВозниклоИсключение Тогда
					Возврат;
				КонецЕсли;
			КонецЦикла;
			
			ЗавершитьВыполнение = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗавершитьВыполнение Тогда
		ОтключитьОбработчикОжидания("ТЦОжиданиеЗавершенияВыполнения");
		
		Если ТЦЭтоИзолированныйРежимРаботы Тогда
			Если ТЦКоличествоОшибок = 0 Тогда
				ТЦОбщийРезультат = "Тесты пройдены без ошибок";
			Иначе 
				ТЦОбщийРезультат = "Количество ошибок: " + ТЦКоличествоОшибок;
			КонецЕсли;			
		КонецЕсли;
		
		Если ТЦКонтекстВыполнения.ЗавершатьРаботуПослеВыполнения Тогда
			ЗавершитьРаботуСистемы(Ложь, Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Служебная функция, предназначенная для выполнения указанной процедуры и записи результатов в текстовый файл.
//   Параметры:
//      ИмяПроцедуры - имя тестовой процедуры, которую нужно выполнить.
//						
Функция ТЦВыполнитьДействиеТеста(Знач ИмяФункции)
	
	ТЦКонтекст().ТекущийРезультатВыполнения = Неопределено;
	ТЦКонтекстВыполнения.ВозниклоИсключение = Ложь;
	ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки = "";
	ТЦКонтекстВыполнения.ТекущаяОперация = ИмяФункции;
	ТекстОшибкиПользователю = "";
	
	Если ТЦЭтоИзолированныйРежимРаботы Тогда
		ТЦКонтекстВыполнения.НачалоТекущегоЗамера = ТЦПолучитьВремяДляЗамера();
	КонецЕсли;
	
	Попытка
		РезультатВыполнения = Вычислить(ИмяФункции);
		
		Если ТЦЭтоИзолированныйРежимРаботы Тогда
			ТЦКонтекстВыполнения.ОкончаниеТекущегоЗамера = ТЦПолучитьВремяДляЗамера();
			Длительность = ТЦКонтекстВыполнения.ОкончаниеТекущегоЗамера-ТЦКонтекстВыполнения.НачалоТекущегоЗамера;
			Если ТЦЕстьВремяВМиллисекундах() Тогда
				Длительность = Длительность / 1000;
			КонецЕсли;
		КонецЕсли;

		Если РезультатВыполнения = ТЦРезультатВыполненияОшибка() Тогда
			ТЦВозниклоИсключение = Ложь;
			
			Если ТЦЭтоИзолированныйРежимРаботы Тогда
				
				ТекстОшибкиПользователю = "Функция вернула ошибку. " + ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки + " Длительность="
					+ Формат(Длительность,"ЧДЦ=2; ЧРГ=.; ЧН=0") + " с";
				
				ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки = "Действие=" + ИмяФункции + " Длительность="
					+ Формат(Длительность,"ЧДЦ=2; ЧРГ=.; ЧН=0") + " с"
					+ " ОшибкаПриВыполнении=" + Символы.ПС + "Функция вернула ошибку. " + ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки;
			Иначе
				ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки = "Возникла ошибка при выполнении " + ИмяФункции + Символы.ПС +
															"Функция вернула ошибку. " + ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки;				
			КонецЕсли;
				
				
		КонецЕсли;
		
	Исключение
		
		РезультатВыполнения = ТЦКонтекст().РезультатВыполнения.Неопределено;
		ТЦКонтекстВыполнения.ВозниклоИсключение = Истина;		
		
		Если ТЦЭтоИзолированныйРежимРаботы Тогда
			ТЦКонтекстВыполнения.ОкончаниеТекущегоЗамера = ТЦПолучитьВремяДляЗамера();
			Длительность = ТЦКонтекстВыполнения.ОкончаниеТекущегоЗамера-ТЦКонтекстВыполнения.НачалоТекущегоЗамера;
			Если ТЦЕстьВремяВМиллисекундах() Тогда
				Длительность = Длительность / 1000;
			КонецЕсли;
			
			ТекстОшибкиПользователю = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()) + " Длительность="
				+ Формат(Длительность, "ЧДЦ=2; ЧРГ=.; ЧН=0") + " с";
			
			ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки = "Действие=" + ИмяФункции + " Длительность="
				+ Формат(Длительность, "ЧДЦ=2; ЧРГ=.; ЧН=0") + " с"
				+ " ОшибкаПриВыполнении=" + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Иначе
			ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки = "Возникла ошибка при выполнении " + ИмяФункции + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецЕсли;
			
	КонецПопытки;
				
	Если ТЦКонтекстВыполнения.ВозниклоИсключение ИЛИ РезультатВыполнения = ТЦРезультатВыполненияОшибка() Тогда
		
		Если ТЦКонтекстВыполнения.ПоказыватьОшибкиИСообщения И НЕ ТЦЭтоИзолированныйРежимРаботы Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки;
			Сообщение.Сообщить();
		КонецЕсли;
		
		ТЦЗаписатьВЖурнал(ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки, "Тест-центр", ТЦСтатусСообщенияОшибка());
		Если ТЦЭтоИзолированныйРежимРаботы Тогда
			ТЦЗаписьРезультатовВФайлНаСервере(ТЦКонтекстВыполнения.ТекстИсключенияИлиОшибки);
			ТЦКоличествоОшибок = ТЦКоличествоОшибок + 1;
			ТЦПоказазатьОшибку(ТЦКонтекстВыполнения.ТекущаяОперация, ТекстОшибкиПользователю);
		КонецЕсли;
		
		Если ТЦЭтоИзолированныйРежимРаботы Тогда
			Если ТЦКоличествоОшибок = 0 Тогда
				ТЦОбщийРезультат = "Тесты пройдены без ошибок";
			Иначе 
				ТЦОбщийРезультат = "Количество ошибок: " + ТЦКоличествоОшибок;
			КонецЕсли;			
		КонецЕсли;
		
		Если ТЦКонтекстВыполнения.ЗавершатьРаботуПослеВыполнения Тогда
			ЗавершитьРаботуСистемы(Ложь, Ложь);
		КонецЕсли;
		
	ИначеЕсли РезультатВыполнения = ТЦКонтекст().РезультатВыполнения.Продолжить Тогда
		ПодключитьОбработчикОжидания("ТЦОжиданиеЗавершенияВыполнения", 1);
	Иначе
		
		Если ТЦЭтоИзолированныйРежимРаботы Тогда
			ТЦЗаписьРезультатовВФайлНаСервере("Действие=" + ИмяФункции + " Длительность="
				+ Формат(Длительность, "ЧДЦ=2; ЧРГ=.; ЧН=0") + " с");
			ТЦПоказазатьЗамер(ИмяФункции, Длительность);
		КонецЕсли;
			
	КонецЕсли;
	
	ТЦКонтекстВыполнения.ТекущийРезультатВыполнения = РезультатВыполнения;
	Возврат РезультатВыполнения;
		
КонецФункции	// ВыполнитьДействиеТеста

&НаКлиенте
// Возвращает текущую универсальную дату в миллисекундах, либо текущее время в за
// в зависимости от версии платформы
Функция ТЦПолучитьВремяДляЗамера()
	
	Попытка
		Время = Вычислить("ТекущаяУниверсальнаяДатаВМиллисекундах()");
	Исключение
		Время = ТекущаяДата();
	КонецПопытки;
	
	Возврат Время;
	
КонецФункции

&НаКлиенте
// Истина если, если система позволяет получать универсальную дату в миллисекундах
Функция ТЦЕстьВремяВМиллисекундах()
	
	Результат = Истина;	
	Попытка
		Время = Вычислить("ТекущаяУниверсальнаяДатаВМиллисекундах()");
	Исключение
		Результат = Ложь;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
// Создает контекст для изолированного исполнения
Функция ТЦСоздатьКонтекстДляИзолированногоИсполнения()
	
	Статус = Новый Структура;
	Статус.Вставить("Информация", 1);
	Статус.Вставить("Предупреждение", 2);
	Статус.Вставить("Ошибка", 3);
	
	Результат = Новый Структура;
	Результат.Вставить("Успешно", 1);
	Результат.Вставить("Предупреждение", 2);
	Результат.Вставить("Ошибка", 3);
	Результат.Вставить("Продолжить", 4);
	Результат.Вставить("Неопределено", 5);
	
	Контекст = Новый Структура;
	
	Контекст.Вставить("ВозниклоИсключение", Ложь);
	Контекст.Вставить("ТекстИсключенияИлиОшибки");
	Контекст.Вставить("ТекущаяОперация");
	Контекст.Вставить("МассивДействий");
	Контекст.Вставить("КоэффициентИнтенсивности", 1);
	Контекст.Вставить("НачалоТекущегоЗамера");
	Контекст.Вставить("ОкончаниеТекущегоЗамера");
	Контекст.Вставить("ЗавершатьРаботуПослеВыполнения", Ложь);
	Контекст.Вставить("ПоказыватьОшибкиИСообщения", Ложь);
	
	Контекст.Вставить("СтатусСообщения", Статус);
	Контекст.Вставить("ТекущийРезультатВыполнения");
	Контекст.Вставить("РезультатВыполнения", Результат);
	Контекст.Вставить("Результаты", Новый Соответствие);
	Контекст.Вставить("ВРМ", Новый Структура("Роль, ИмяРоли, Компьютер, Пользователь, Номер, Сценарий"));
	Контекст.Вставить("ФормаВРМ");
	
	Возврат Контекст;	
	
КонецФункции

&НаСервере
// Записывает информацию о системе в указанный файл на сервере
Процедура ТЦЗаписатьСистемнуюИнформациюВФайлНаСервере(Знач ИмяФайла = "")
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	
	Если СокрЛП(ИмяФайла) = "" Тогда
		ИмяФайла = СокрЛП(ТЦОбъект.ТЦИмяФайлаЖурналаТестирования);
	КонецЕсли;
	
	Текст = Новый ЗаписьТекста;
	ВыбранныйФайл = Новый Файл(ИмяФайла);
	Если ВыбранныйФайл.Существует() Тогда
		Текст.Открыть(ВыбранныйФайл.ПолноеИмя, КодировкаТекста.UTF8,,Истина);	
	Иначе
		СоздатьКаталог(ВыбранныйФайл.Путь);
		Текст.Открыть(ИмяФайла, КодировкаТекста.UTF8,,Истина);
	КонецЕсли;
	
	СтрокаДляЗаписи = Символы.ПС;
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "Начало теста", Формат(ТекущаяДата(),"ДЛФ=DT"));
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "ВерсияПлатформы", СистемнаяИнформация.ВерсияПриложения);
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "ИмяКонфигурации", Метаданные.Имя);
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "СинонимИмениКонфигурации", Метаданные.Синоним);
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "ВерсияКонфигурации", Метаданные.Версия);
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "ИнтерфейсКонфигурации", Метаданные.Интерфейсы);
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "ИнформацияПрограммыПросмотра", СистемнаяИнформация.ИнформацияПрограммыПросмотра);
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "Процессор", СистемнаяИнформация.Процессор);
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "ОперативнаяПамять", СистемнаяИнформация.ОперативнаяПамять);
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "ТипОС", Строка(СистемнаяИнформация.ТипПлатформы));
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "ВерсияОС", СистемнаяИнформация.ВерсияОС);
	ТЦДополнитьСтрокуИнфо(СтрокаДляЗаписи, "ИмяМодуля", ТЦПолучитьИмяМодуля());

	Текст.ЗаписатьСтроку(СтрокаДляЗаписи);
	Текст.Закрыть();	
	
КонецПроцедуры

// Служебная процедура, показывает сообщение об ошибке в изолированном режиме
//
// Параметры:
//  ИмяОперации			- Строка
//  СообщениеОбОшибке	- Строка
//
&НаКлиенте
Процедура ТЦПоказазатьОшибку(Знач ИмяОперации, Знач СообщениеОбОшибке)
	
	Если ТЦКонтекст().ПоказыватьОшибкиИСообщения Тогда
		СтрокаРезультата		= ТЦРезультаты.Добавить();
		СтрокаРезультата.Тест	= ИмяОперации;
		СтрокаРезультата.Результат = Ложь;
		СтрокаРезультата.Сообщение = СообщениеОбОшибке;
	КонецЕсли;
	
КонецПроцедуры

// Служебная процедура, показывает замер в изолированном режиме
//
// Параметры:
//  ИмяОперации	 - Строка
//  ДлительностьВСекундах - Число
//
&НаКлиенте
Процедура ТЦПоказазатьЗамер(Знач ИмяОперации, Знач ДлительностьВСекундах)
	
	Если ТЦКонтекст().ПоказыватьОшибкиИСообщения Тогда
		СтрокаРезультата		= ТЦРезультаты.Добавить();
		СтрокаРезультата.Тест	= ИмяОперации;
		СтрокаРезультата.Результат = Истина;
		СтрокаРезультата.Сообщение = Формат(ДлительностьВСекундах, "ЧДЦ=2; ЧРГ=.; ЧН=0") + " с";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
//Служебная процедура, предназначенная для записи результатов в текстовый файл
// Параметры:
//  Результат - Строка с результатом, которая будет записана в файл
// 
//  ИмяФайла - каталог и файл, в который будут сохраняться результаты
//	
// Т.к процедура работает НаСервере, то ИмяФайла инициализируется на сервере в процедуре ПриСозданииНаСервере
// В случае необходимости переноса процедуры на клиента требуется перенести инициализацию из ПриСозданииНаСервере
// в начало процедуры ПриОткрытии
Процедура ТЦЗаписьРезультатовВФайлНаСервере(Знач Результат, Знач ИмяФайла="")
	
	Если СокрЛП(ИмяФайла) = "" Тогда
		ИмяФайла = СокрЛП(ТЦОбъект.ТЦИмяФайлаЖурналаТестирования);
	КонецЕсли;
	
	СтрокаДляЗаписи = Формат(ТекущаяДата(),"ДЛФ=DT") + " " + Результат;
	
	Текст = Новый ЗаписьТекста;
	ВыбранныйФайл = Новый Файл(ИмяФайла);
	Если ВыбранныйФайл.Существует() Тогда
		Текст.Открыть(ВыбранныйФайл.ПолноеИмя, КодировкаТекста.UTF8,,Истина);	
	Иначе
		СоздатьКаталог(ВыбранныйФайл.Путь);
		Текст.Открыть(ИмяФайла, КодировкаТекста.UTF8,,Истина);
	КонецЕсли;
	
	Текст.ЗаписатьСтроку(СтрокаДляЗаписи);
	Текст.Закрыть();

КонецПроцедуры	// ЗаписьРезультатовВФайл

&НаКлиентеНаСервереБезКонтекста
// Возвращает текущего модуля 
Функция ТЦПолучитьИмяМодуля()
	
	Попытка
		ВызватьИсключение Истина;
	Исключение
		Инфо = ИнформацияОбОшибке();
		Возврат Инфо.ИмяМодуля;
	КонецПопытки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
//Служебная процедура, предназначенная для составления строки с параметрами 
//   Параметры:
//      ДополняемаяСтрока - строка, в которую дополняются параметры
//      НаименованиеПараметра - наименование параметра
//      ЗначениеПараметра - значение параметра
//
Процедура ТЦДополнитьСтрокуИнфо(ДополняемаяСтрока, НаименованиеПараметра, ЗначениеПараметра)

	Если НЕ ЗначениеЗаполнено(ЗначениеПараметра) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДополняемаяСтрока) Тогда
		ДополняемаяСтрока = ДополняемаяСтрока + Символы.ПС;
	КонецЕсли;
	
	ДополняемаяСтрока = ДополняемаяСтрока + НаименованиеПараметра + "=" + ЗначениеПараметра; 

КонецПроцедуры	// ДополнитьСтрокуИнфо()

&НаКлиентеНаСервереБезКонтекста
// Преобразовать объект типа ИнформацияОбОшибке в строку
//
// Параметры:
//  Ошибка - ИнформацияОбОшибке
//
// Возвращаемое значение:
//  Строка - Строковое предстваление ошибки
//
Функция ТЦИнформациюОбОшибкеВСтроку(Ошибка, НомерПричины = 0) 
	
	ТекстОшибки = "";
	
	Если Не ПустаяСтрока(Ошибка.ИмяМодуля) Тогда
		ТекстОшибки = ТекстОшибки + "{"
			+ Ошибка.ИмяМодуля + "("
			+ Ошибка.НомерСтроки + ")}: ";
	КонецЕсли;
	
	ТекстОшибки = ТекстОшибки
		+ Ошибка.Описание + "
		|" + Ошибка.ИсходнаяСтрока;
	
	Если Ошибка.Причина <> Неопределено Тогда
		ТекстОшибки = ТекстОшибки + "
			|
			|ПРИЧИНА №" + Формат(НомерПричины + 1, "ЧГ=0") + "
			|" + ТЦИнформациюОбОшибкеВСтроку(Ошибка.Причина, НомерПричины + 1);
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции // ИнформациюОбОшибкеВСтроку()

&НаКлиентеНаСервереБезКонтекста 
// Записать Сообщение в журнал регистрации
Процедура ТЦЗаписатьВЖурнал(Сообщение, Событие = Неопределено, Важность = Неопределено)
	
	ЭтоОшибка = ТипЗнч(Сообщение) = Тип("ИнформацияОбОшибке")
	        Или ТипЗнч(Сообщение) = Тип("Структура");
	
	Если ЭтоОшибка Тогда
		ТекстОшибки = ТЦИнформациюОбОшибкеВСтроку(Сообщение);
	Иначе
		ТекстОшибки = Сообщение;
	КонецЕсли;
	
	ТЦЗаписатьВЖурналНаСервере(
		ТекстОшибки,
		Событие,
		Важность,
		ЭтоОшибка);	
	
КонецПроцедуры

&НаСервереБезКонтекста
// Записать Сообщение в журнал регистрации
Процедура ТЦЗаписатьВЖурналНаСервере(ТекстОшибки, Событие, Важность, ЭтоОшибка)
	
	Имя = ?(Событие = Неопределено, "Тест-центр", Событие);
	
	Если Важность = Неопределено Тогда
		Если ЭтоОшибка Тогда
			Уровень = УровеньЖурналаРегистрации.Ошибка;
		Иначе
			Уровень = УровеньЖурналаРегистрации.Информация;
		КонецЕсли;
	Иначе
		Если Важность = 3 Тогда
			Уровень = УровеньЖурналаРегистрации.Ошибка;
		ИначеЕсли Важность = 2 Тогда
			Уровень = УровеньЖурналаРегистрации.Предупреждение;
		ИначеЕсли Важность = 1 Тогда
			Уровень = УровеньЖурналаРегистрации.Информация;
		КонецЕсли;
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(Имя, Уровень,, "Тест-центр", ТекстОшибки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
// Возвращает ссылку на модуль или Неопределено
Функция ТЦПолучитьОбщийМодульПоИмени(ИмяМодуля)
	
	Попытка
		НужныйМодуль = Вычислить(ИмяМодуля);
		
		#Если Не ВебКлиент Тогда
		Если ТипЗнч(НужныйМодуль) <> Тип("ОбщийМодуль") Тогда
			НужныйМодуль = Неопределено;
		КонецЕсли;
		#КонецЕсли
	
	Исключение
		НужныйМодуль = Неопределено;
	КонецПопытки;

	Возврат НужныйМодуль;
	
КонецФункции

// Процедура-заглушка, позволяющая исключить неявно используемые
// и библиотечные процедуры и функции из отчета диагностики «Проверка конфигурации».
// Не используется в исполняемом коде.
//
&НаКлиенте
Процедура ПроцедурыИФукнцииИспользуемыеНеявно()
	
	Если Истина Тогда
		Возврат;
	КонецЕсли;
	
	ТЦЗаписатьПоказатель(Неопределено, Неопределено);
	ТЦИмяКомпьютера();
	ТЦИмяПользователя();
	ТЦИмяРоли();
	ТЦКоэффициентИнтенсивности();
	ТЦНомерВРМ();
	ТЦРезультатВыполненияПредупреждение();
	ТЦРезультатВыполненияПродолжить(Неопределено, Неопределено, Неопределено);
	ТЦРоль();
	ТЦСтатусСообщенияИнформация();
	ТЦСтатусСообщенияПредупреждение();
	ТЦСценарий();
	ПроцедурыИФукнцииИспользуемыеНеявно();
	
КонецПроцедуры

#КонецОбласти

#Область Служебные_процедуры_и_функции_для_связки_с_Vanessa_Automation

&НаКлиенте
// Открывает форму Vanessa-Automation из "Дополнительных отчетов и обработок"
//
Процедура ОткрытьФормуVA()
	ФайлФичи = Новый Файл(ТЦОбъект.ПутьКФиче);
		
	ИмяОбработки = ДополнительныеОтчетыИОбработкиВызовСервера.ПодключитьВнешнююОбработку(ТЦОбъект.VA);
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("КаталогПроекта", ФайлФичи.Путь); 
	
    ФормаVA = ПолучитьФорму(
		"ВнешняяОбработка." + ИмяОбработки + ".Форма", 
		ПараметрыОткрытия, 
		ЭтотОбъект, 
		Новый УникальныйИдентификатор
	);

	ФормаVA.Объект.ЗагрузкаФичПриОткрытии = "Не загружать";
	
	ОбнулитьНастройкиVA();
	
	ФормаVA.Открыть();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНастройкиVA()

	ФормаVA.Объект.КаталогФич = ТЦОбъект.ПутьКФиче;
	ФормаVA.Объект.ЗакрытьTestClientПослеЗапускаСценариев = ТЦОбъект.ЗакрытьTestClientПослеЗапускаСценариев;
	ФормаVA.Объект.ТаймаутЗапуска1С = 60;
	ФормаVA.ЗапрашиватьПодтверждениеПриЗакрытии = "Нет";
	ФормаVA.Объект.ОбновлятьСтатистикуВДереве = Ложь;
	ФормаVA.Объект.ОбновлятьДеревоПриНачалеВыполненияСценария = Ложь;
	ФормаVA.Объект.ДелатьЛогВыполненияСценариевВЖР = Истина;

	НачальныйПорт = 48000;
	СмещениеПорта = ТЦРеальныйНомерВРМ(Истина);
	КоличествоПортовНаВРМ = 10;
	
	НачальныйПорт = НачальныйПорт + СмещениеПорта * КоличествоПортовНаВРМ;
	
	ФормаVA.Объект.ДиапазонПортовTestclient = 
		Формат(НачальныйПорт, "ЧГ=") 
		+ "-" 
		+ Формат(НачальныйПорт + КоличествоПортовНаВРМ - 1, "ЧГ=");
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиНастройкиРолиВКонтекстVA()
	
	ФормаVA.ОбъектКонтекстСохраняемый.Вставить("ИмяПользователя", ТЦИмяПользователя());
	ФормаVA.ОбъектКонтекстСохраняемый.Вставить("ПарольПользователя", ТЦПарольПользователя(ТЦИмяПользователя()));
	
	Для Каждого Переменная Из ТЦОбъект.ДополнительныеПеременные Цикл
		ВставляемоеЗначение = Переменная.Значение;
		Если Переменная.Массив Тогда
			ВставляемоеЗначение = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ВставляемоеЗначение, ";");
			Если Переменная.Перемешивать Тогда
				ТЦПеремешатьМассив(ВставляемоеЗначение);
			КонецЕсли;
		КонецЕсли;
		ФормаVA.ОбъектКонтекстСохраняемый.Вставить(Переменная.Имя, ВставляемоеЗначение);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьVAКВыполнениюСценариев()
	ФормаVA.КомандаПерезагрузить();
КонецПроцедуры

&НаКлиенте
// Обработчик ожидания для метода ТЦРезультатВыполненияПродолжить.
// Проверяет текущее состояние прогона сценариев VA.
//
// В случае ошибки и взведенного флага "ОстанавливатьТестПриОшибке" прерывает
// выполнение сценариев и завершает полезную работу ВРМ.
//
// В случае успешного завершения сценария VA или при подавляемой ошибке
// заново запускает прогон сценариев через паузу размером с "ПаузаМеждуЗапускомФичи" секунд.
//
// Сбрасывает кэш имеющихся замеров производительности подсистемы "Оценка производительности".
//
//  Возвращаемое значение:
//   Неопределено - Функция всегда возвращает Неопределено.
//
Функция ОбработчикОжиданияВыполненияСценариев() Экспорт

	Если ФормаVA.Объект.ИдетВыполнениеСценариев Тогда
		ТЦЗаписатьВЖурнал(ТЦИмяРоли() + ". Идет выполнение сценариев");
		Возврат Неопределено;
	КонецЕсли;
	
	// Если замер когда-то был начат
	Если ОценкаПроизводительностиЗамерВремени <> Неопределено Тогда
		// Сбросим сохраненные данные по всем замерам, не записанным явно.
		ОценкаПроизводительностиЗамерВремени = Неопределено;
	КонецЕсли;
	
	ВозниклаОшибка = НЕ ФормаVA.СтатусЗапускаСценариев;
	ТЦЗаписатьВЖурнал(ТЦИмяРоли() + ". Результат прогона набора сценариев: " + (НЕ ВозниклаОшибка));
	
	Если ТЦОбъект.ОстанавливатьТестПриОшибке И ВозниклаОшибка Тогда
		ТЦЗавершитьВыполнение(ТЦРезультатВыполненияОшибка());
	Иначе
		ТЦПауза(ТЦОбъект.ПаузаМеждуЗапускомФичи);
		
		НачатьВыполнениеСценария();
	КонецЕсли;
	
КонецФункции

&НаКлиенте
// Обработчик ожидания. Проверяет, что сценарий тестирования был прерван или завершен,
// и останавливает выполнение сценариев VA.
//
Процедура ПроверитьПрерываниеСценария() Экспорт
	
	ИнформацияОТекущемТесте = ИнформацияОТекущемТесте();
	
	Если ИнформацияОТекущемТесте.Остановлен Тогда
		ОтключитьОбработчикОжидания("ПроверитьПрерываниеСценария");
		
		ОстановитьВыполнениеСценария();
		ТЦЗавершитьВыполнение(ТЦРезультатВыполненияУспешно());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбнулитьНастройкиVA()
	
	КлючОбъекта = ФормаVA.ПолучитьИДСохраненияОбщихНастроек();
	КлючНастроек = ФормаVA.ПолучитьПрефиксИнструмента() + "ДанныеКлиентовТестирования";
	
	ОбнулитьНастройкиVAНаСервере(КлючОбъекта, КлючНастроек);

КонецПроцедуры

&НаСервере
Процедура ОбнулитьНастройкиVAНаСервере(КлючОбъекта, КлючНастроек)
	
	ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, КлючНастроек, Неопределено);
	
КонецПроцедуры


&НаКлиенте
// Передача VA команды начала выполнения сценариев.
//
Процедура НачатьВыполнениеСценария()
	ОчиститьСообщения();
	ПередНачаломВыполненияСценарияVA();
	ФормаVA.КомандаВыполнитьСценарии();	
КонецПроцедуры

&НаКлиенте
// Передача VA команды остановки выполнения сценариев.
//
Процедура ОстановитьВыполнениеСценария()
	ФормаVA.КомандаОстановитьСценарии();
КонецПроцедуры


&НаСервереБезКонтекста
Функция ИнформацияОТекущемТесте()
	
	Результат = Новый Структура;
	Результат.Вставить("Остановлен", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТЦТест.Прерван
		|		ИЛИ ТЦТест.Результат = ЗНАЧЕНИЕ(Перечисление.ТЦРезультатВыполнения.Ошибка)
		|		ИЛИ ТЦТест.Результат = ЗНАЧЕНИЕ(Перечисление.ТЦРезультатВыполнения.Успешно) КАК Остановлен
		|ИЗ
		|	Документ.ТЦТест КАК ТЦТест
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТЦТест.Дата УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

#КонецОбласти
